@Metadata.layer: #CORE

@UI:{
    headerInfo:{ typeName: 'Booking',
                 typeNamePlural: 'Bookings',
                 title:{ type:#STANDARD,
                 value: 'BookingID' }}}
annotate view zc_booking_1031 with
{
  @Search.defaultSearchElement: true
  @UI.facet: [{ id: 'Booking',
                purpose: #STANDARD,
                type: #IDENTIFICATION_REFERENCE,
                label:'Booking',
                position: 10}]
  TravelId;
  @Search.defaultSearchElement: true
  @UI: { lineItem:       [ { position: 20,
                             importance: #HIGH } ],
         identification: [ { position: 20 } ] }
  BookingId;
  @UI: { lineItem:       [ { position: 30,
                           importance: #HIGH } ],
       identification: [ { position: 30 } ] }
  BookingDate;

  @Consumption.valueHelpDefinition: [{entity: {name: '/DMO/I_Customer_StdVH', element: 'CustomerID' }, useForValidation: true}]
  @Search.defaultSearchElement: true
  @UI: { lineItem:       [ { position: 40,
                            importance: #HIGH } ],
        identification: [ { position: 40 } ] }
  CustomerId;

  @Consumption.valueHelpDefinition: [
      { entity: {name: '/DMO/I_Flight_StdVH', element: 'AirlineID'},
        additionalBinding: [ { localElement: 'FlightDate',   element: 'FlightDate',   usage: #RESULT},
                             { localElement: 'ConnectionID', element: 'ConnectionID', usage: #RESULT},
                             { localElement: 'FlightPrice',  element: 'Price',        usage: #RESULT},
                             { localElement: 'CurrencyCode', element: 'CurrencyCode', usage: #RESULT } ],
        useForValidation: true }
    ]
  @UI: { lineItem:       [ { position: 50,
                         importance: #HIGH } ],
     identification: [ { position: 50 } ] }
  AirlineId;
  @Consumption.valueHelpDefinition: [
  { entity: {name: '/DMO/I_Flight_StdVH', element: 'ConnectionID'},
    additionalBinding: [ { localElement: 'FlightDate',   element: 'FlightDate',   usage: #RESULT},
                         { localElement: 'AirlineID',    element: 'AirlineID',    usage: #FILTER_AND_RESULT},
                         { localElement: 'FlightPrice',  element: 'Price',        usage: #RESULT},
                         { localElement: 'CurrencyCode', element: 'CurrencyCode', usage: #RESULT } ],
    useForValidation: true }
  ]
  @UI: { lineItem:       [ { position: 60,
                             importance: #HIGH } ],
         identification: [ { position: 60 } ] }
  ConnectionId;

  @Consumption.valueHelpDefinition: [
  { entity: {name: '/DMO/I_Flight_StdVH', element: 'FlightDate'},
   additionalBinding: [ { localElement: 'AirlineID',    element: 'AirlineID',    usage: #FILTER_AND_RESULT},
                        { localElement: 'ConnectionID', element: 'ConnectionID', usage: #FILTER_AND_RESULT},
                        { localElement: 'FlightPrice',  element: 'Price',        usage: #RESULT},
                        { localElement: 'CurrencyCode', element: 'CurrencyCode', usage: #RESULT } ],
   useForValidation: true }
  ]
  @UI: { lineItem:       [ { position: 70,
                            importance: #HIGH } ],
        identification: [ { position: 70 } ] }

  FlightDate;
  @Consumption.valueHelpDefinition: [
     { entity: {name: '/DMO/I_Flight_StdVH', element: 'Price'},
       additionalBinding: [ { localElement: 'FlightDate',   element: 'FlightDate',   usage: #FILTER_AND_RESULT},
                            { localElement: 'AirlineID',    element: 'AirlineID',    usage: #FILTER_AND_RESULT},
                            { localElement: 'ConnectionID', element: 'ConnectionID', usage: #FILTER_AND_RESULT},
                            { localElement: 'CurrencyCode', element: 'CurrencyCode', usage: #RESULT } ],
       useForValidation: true }
   ]
  @UI: { lineItem:       [ { position: 80,
                          importance: #HIGH } ],
                          identification: [ { position: 80 } ] }
  FlightPrice;
  @Consumption.valueHelpDefinition: [{entity: {name: 'I_CurrencyStdVH', element: 'Currency' }, useForValidation: true }]
  CurrencyCode;
}